function prompt_keithy_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd vcs_info

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:*' check-for-changes true
  zstyle ':vcs_info:*:*' unstagedstr '%F{magenta}%B!%b%f'
  zstyle ':vcs_info:*:*' stagedstr '%F{green}%B+%b%f'
  zstyle ':vcs_info:*:*' actionformats "%F{blue}%s%f:%F{green}%b%f %u%c %F{yellow}%B(%a)%b%f"
  zstyle ':vcs_info:*:*' formats "%F{blue}%s%f:%F{green}%b%f %u%c"

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%f%b'

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{red}%n%f@%F{yellow}%m%f "}%F{cyan}%c%f%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}↵ [%?]%f)${VIM:+" %B%F{green}V%f%b"} ${vcs_info_msg_0_}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_keithy_setup "$@"
