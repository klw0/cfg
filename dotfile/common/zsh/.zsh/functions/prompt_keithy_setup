MAX_VCS_DELAY=250

shorten_path() {
    local path=$1

    local short_path=""
    local elements=("${(s:/:)path}")
    for e in "${elements[@]:0:-1}"; do
        short_path+="${e:0:1}/"
    done
    short_path+="${elements[@]: -1}"

    echo "${short_path}"
}

prompt_keithy_setup() {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr sp percent subst)

    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info
    autoload -Uz vcs_info_hookadd
    zmodload zsh/datetime

    add-zsh-hook precmd vcs_info
    vcs_info_hookadd pre-get-data pre_get_data
    vcs_info_hookadd post-backend post_backend

    zstyle ":vcs_info:*" enable git cvs
    zstyle ":vcs_info:*:*" check-for-changes true
    zstyle ":vcs_info:*:*" unstagedstr "%B!%b"
    zstyle ":vcs_info:*:*" stagedstr "%B+%b"
    zstyle ":vcs_info:*:*" actionformats "%s:%b %u%c (%a)"
    zstyle ":vcs_info:*:*" formats "%s:%b %u%c"

    PROMPT='%F{cyan}${SSH_TTY:+"%m:"}$(shorten_path "$(print -P %~)")%(!.#. %B${${${KEYMAP:-main}/vicmd/❮}/[^❮]*/❯}%b) %f'
    RPROMPT='%(?.. %F{red}↵ [%?]%f) ${vcs_info_msg_0_}'
}

+vi-pre_get_data() {
    user_data[start_time]=$EPOCHREALTIME
}

+vi-post_backend() {
    local duration=$(( (${EPOCHREALTIME} - ${user_data[start_time]}) * 1000 ))
    if [[ "${duration}" -gt "${MAX_VCS_DELAY}" ]]; then
        # XXX(klw0): This can clobber for repos with the same name.
        zstyle ":vcs_info:*:*:${context##*:}" check-for-changes false
    fi
}

prompt_keithy_setup "$@"
