function shorten_path() {
    local path=$1

    local short_path=""
    local elements=("${(s:/:)path}")
    for e in "${elements[@]:0:-1}"; do
        short_path+="${e:0:1}/"
    done
    short_path+="${elements[@]: -1}"

    echo "${short_path}"
}

function prompt_keithy_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr sp percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd vcs_info

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:*' check-for-changes true
  zstyle ':vcs_info:*:*' unstagedstr "%B!%b"
  zstyle ':vcs_info:*:*' stagedstr "%B+%b"
  zstyle ':vcs_info:*:*' actionformats "%s:%b %u%c (%a)"
  zstyle ':vcs_info:*:*' formats "%s:%b %u%c"

  PROMPT='%F{cyan}${SSH_TTY:+"%m:"}$(shorten_path "$(print -P %~)")%(!.#. %B${${${KEYMAP:-main}/vicmd/❮}/[^❮]*/❯}%b) %f'
  RPROMPT='%(?.. %F{red}↵ [%?]%f) ${vcs_info_msg_0_}'
}

prompt_keithy_setup "$@"
